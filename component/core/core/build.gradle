apply from: rootProject.file(files.build_library_gradle)

// 是否打印日志
boolean openLog = isOpenLog.toBoolean()
// 是否 Debug 模式
boolean openDebug = isOpenDebug.toBoolean()
// 是否开启优化检测、调试工具 ( 控制 UETool、LeakCanary、BlockCanary、Glance 等开关 )
boolean showDebugTools = showDebugTools.toBoolean()
// 升级版本号 ( 视情况而定, 为了区分 versionCode )
long upgradeCode = upgradeCode.toLong()

android.defaultConfig {
    buildConfigField("boolean", "openLog", "$openLog")
    buildConfigField("boolean", "openDebug", "$openDebug")
    buildConfigField("boolean", "showDebugTools", "$showDebugTools")
    buildConfigField("long", "upgradeCode", "${upgradeCode}")
}

/**
 * 该 Module 依赖 core 核心开发库、核心第三方库等
 * 统一维护核心库依赖, 对外只需要依赖该 Module 便可使用整个核心模块 ( core 文件以及内部所有 libs )
 */
dependencies {

    // 核心基础依赖库
    api project(':core_base_lib')

    // ===================
    // = core - 核心开发库 =
    // ===================

    // 基础 ( 基类等 ) lib
    api project(':core_lib_base')
    // 通用实体类 lib
    api project(':core_lib_bean')
    // 通用配置、常量 lib
    api project(':core_lib_config')
    // 通用 Engine ( 图片加载、日志、JSON、权限、资源选择 等 ) lib
    api project(':core_lib_engine')
    // 网络框架 lib
    api project(':core_lib_network')
    // 通用 UI 样式、资源、交互、控件 lib
    api project(':core_lib_ui')
    // 通用工具库
    api project(':core_lib_utils')

    // =============
    // = 第三方库依赖 =
    // =============

    // 瓦力多渠道打包 https://github.com/Meituan-Dianping/walle
    api deps.build_apk.walle

    // Bugly https://bugly.qq.com/docs/
    api deps.property.bugly
    api deps.property.bugly_ndk

    // ARouter 路由 https://github.com/alibaba/ARouter
    api deps.lib.arouter_api // https://github.com/alibaba/ARouter/blob/master/README_CN.md
    kapt deps.lib.arouter_compiler

    // ====================
    // = 性能检测、排查相关库 =
    // ====================

    // 是否编译调试工具
    if (showDebugTools) {
        // 内存检测工具 https://github.com/square/leakcanary
        api deps.property.leakcanary
        // 应用数据库展示 https://github.com/guolindev/Glance
        api deps.property.glance
        // 饿了么 UETool https://github.com/eleme/UETool/blob/master/README_zh.md
        api deps.property.uetool
        // BlockCanary 性能监控组件 https://github.com/markzhai/AndroidPerformanceMonitor/blob/master/README_CN.md
        api deps.property.blockcanary_android
    } else {
        // 饿了么 UETool https://github.com/eleme/UETool/blob/master/README_zh.md
        api deps.property.uetool_no_op
        // BlockCanary 性能监控组件 https://github.com/markzhai/AndroidPerformanceMonitor/blob/master/README_CN.md
        api deps.property.blockcanary_no_op
    }

//    // 饿了么 UETool https://github.com/eleme/UETool/blob/master/README_zh.md
//    debugApi deps.property.uetool
//    releaseApi deps.property.uetool_no_op
//    // BlockCanary 性能监控组件 https://github.com/markzhai/AndroidPerformanceMonitor/blob/master/README_CN.md
//    debugApi deps.property.blockcanary_android
//    releaseApi deps.property.blockcanary_no_op
}